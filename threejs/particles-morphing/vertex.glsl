#include ./simplexNoise3D

uniform vec2 uResolution;
uniform float uSize;
uniform float uProgress;
uniform vec3 uColorA;
uniform vec3 uColorB;

attribute vec3 aPositionTarget;
attribute float aSize;

varying vec3 vColor;

void main()
{
  // Mixed position
  float noiseOrigin = simplexNoise3D(position * .2);
  float noiseTarget = simplexNoise3D(aPositionTarget * .2);
  float noise = mix(noiseOrigin, noiseTarget, uProgress);
  noise = smoothstep(-1., 1., noise);

  float duartion = .4;
  float delay = (1. - duartion) * noise;
  float end = delay + duartion;
  float progress = smoothstep(delay, end, uProgress);
  vec3 mixedPosition = mix(position, aPositionTarget, progress);

  // Final position
  vec4 modelPosition = modelMatrix * vec4(mixedPosition, 1.);
  vec4 viewPosition = viewMatrix * modelPosition;
  vec4 projectedPosition = projectionMatrix * viewPosition;
  gl_Position = projectedPosition;

  // Point size
  gl_PointSize = aSize * uSize * uResolution.y;
  gl_PointSize *= (1. / - viewPosition.z);

  vColor = mix(uColorA, uColorB, noise);
}